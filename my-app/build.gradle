plugins {
    id 'org.springframework.boot' version '2.0.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
    id 'groovy'
}

group = 'com.test'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
}
ext['groovy.version'] = '2.5.0'

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web-services'

    compile 'com.h2database:h2'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testCompile(group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5')
    testCompile(group: 'org.spockframework', name: 'spock-spring', version: '1.3-groovy-2.5')
}


sourceSets {
    integrationTest {
        groovy.srcDir "$projectDir/src/integration-test/groovy"
        resources.srcDir "$projectDir/src/integration-test/resources"
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    mustRunAfter test
}
check.dependsOn integrationTest
tasks.withType(Test) {
    reports.junitXml.destination = file("${buildDir}/reports/${testResultsDirName}/${name}")
}
test {
    useJUnit()
}

